//gradle --stop
//gradle clean build bintrayUpload --info
plugins {
    id "com.jfrog.bintray" version "1.7"
}
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = "com.mlibrary"
String mArtifactId = "dynamic"

version = "0.0.1"

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile gradleApi()
    compile localGroovy()
    //compile 'com.android.tools.build:gradle-api:2.2.0'
    compile group: 'net.sf.proguard', name: 'proguard-gradle', version: '5.3.1'
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "maven"
        }
    }
    developers {
        developer {
            id "krmao"
            name "michael mao"
            email "767709667@qq.com"
        }
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        MDynamicLib(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            groupId getGroup()
            artifactId mArtifactId
            version version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'gradle plugin for ctrip dynamicapk')
                root.appendNode('name', "MDynamicLib")
                root.appendNode('url', 'https://github.com/krmao/MDynamicHome')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['MDynamicLib']

    dryRun = false //[Default: false] Whether to run this as dry-run, without deploying
    publish = false //[Default: false] Whether version should be auto published after an upload
    override = true //[Default: false] Whether to override version artifacts already published

    pkg {
        repo = 'maven'
        name = "MDynamicLib"
        userOrg = bintrayUser
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/krmao/MDynamicHome'
        vcsUrl = 'https://github.com/krmao/MDynamicHome.git'
        issueTrackerUrl = 'https://github.com/krmao/MDynamicHome/issues'
        labels = ['dynamic', 'android', 'hotfix']
        publicDownloadNumbers = true
        githubRepo = 'https://github.com/krmao/MDynamicHome' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file

        version {
            released = new Date()
            name = "stable"//稳定版
            //desc = 'gradle plugin for ctrip dynamicapk !!'
            vcsTag = '1.3.0'
            attributes = ['gradle-plugin': 'com.mlibrary.dynamic.application ,com.mlibrary.dynamic.library']
        }
    }
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from "${buildDir}/docs"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri("${rootDir}/MLibrarys/MRepo/uploadedArchives"))
            //repository(url: "http://dl.bintray.com/krmao/maven") {
            //    authentication(userName: project.bintrayUser, password: project.bintrayKey)
            //}
            pom.version = version
            pom.artifactId = mArtifactId
            pom.groupId = getGroup()
        }
    }
}
